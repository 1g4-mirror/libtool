# Process with autom4te to create an -*- Autotest -*- test suite.

# Test suite for GNU Libtool.
# Copyright 2004, 2005 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
# 02111-1307, USA.

m4_divert_push([PREPARE_TESTS])dnl
: ${LIBTOOLIZE="${abs_top_builddir}/libtoolize"}
export LIBTOOLIZE
m4_divert_pop([PREPARE_TESTS])dnl

# LT_AT_LIBTOOLIZE([ARGS])
# ------------------------
m4_define([LT_AT_LIBTOOLIZE],
[_lt_pkgvdatadir="$abs_top_srcdir" "$LIBTOOLIZE" $1
])


# LT_AT_CHECK_LIBTOOLIZE(ARGS, [EXIT-STATUS = 0], [STDOUT = `'], [STDERR = `'])
# -----------------------------------------------------------------------------
m4_define([LT_AT_CHECK_LIBTOOLIZE],
[AT_CHECK([LT_AT_LIBTOOLIZE([$1])],
  [$2], [$3], [$4])
])


# LT_AT_BOOTSTRAP
# ---------------
m4_define([LT_AT_BOOTSTRAP],
[
test -f ./ltmain.sh || LT_AT_LIBTOOLIZE([--copy])
test -f ./configure || _lt_pkgvdatadir="$abs_top_srcdir" autoreconf --force --verbose --install
test -f ./configure || exit 1
./configure
])

# We use `dnl' in zillions of places...
m4_pattern_allow([^dnl$])

# We exercise these tools.
AT_TESTED([grep autoreconf autom4te automake])

## ----------- ##
## The suite.  ##
## ----------- ##

AT_INIT

# Torturing subdir-objects builds
m4_include([am-subdir.at])
# Testing func_extract_archives
m4_include([duplicate_members.at])
# Test that inherited_linker_flags in the .la actually gets used.
m4_include([inherited_flags.at])
# stress test
m4_include([stresstest.at])
# C++ templates tests
m4_include([template.at])
# libtoolize tests
m4_include([libtoolize.at])
# link order test
m4_include([link-order.at])

