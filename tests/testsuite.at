# Process with autom4te to create an -*- Autotest -*- test suite.

# Test suite for GNU Libtool.
# Copyright 2004, 2005 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
# 02110-1301, USA.

m4_divert_push([PREPARE_TESTS])dnl
: ${LIBTOOLIZE="${abs_top_builddir}/libtoolize"}
: ${LIBTOOL="${abs_top_builddir}/libtool"}
export LIBTOOLIZE LIBTOOL
eval `$LIBTOOL --config | grep ^EGREP=`
eval `$LIBTOOL --config | $EGREP '(host|build)='`
m4_divert_pop([PREPARE_TESTS])dnl

# LT_AT_LIBTOOLIZE([ARGS])
# ------------------------
m4_define([LT_AT_LIBTOOLIZE],
[_lt_pkgdatadir="$abs_top_srcdir" "$LIBTOOLIZE" $1
])


# LT_AT_CHECK_LIBTOOLIZE(ARGS, [EXIT-STATUS = 0], [STDOUT = `'], [STDERR = `'])
# -----------------------------------------------------------------------------
m4_define([LT_AT_CHECK_LIBTOOLIZE],
[AT_CHECK([LT_AT_LIBTOOLIZE([$1])],
  [$2], [$3], [$4])
])


# LT_AT_BOOTSTRAP
# ---------------
m4_define([LT_AT_BOOTSTRAP],
[
test -f ./ltmain.sh || LT_AT_LIBTOOLIZE([--copy])
test -f ./configure || _lt_pkgdatadir="$abs_top_srcdir" autoreconf --force --verbose --install || exit 1
test -f ./configure || exit 1
./configure
])


# LT_AT_EXEC_CHECK(EXECUTABLE, [STATUS = 0], [STDOUT], [STDERR])
# --------------------------------------------------------------
m4_define([LT_AT_EXEC_CHECK],
[AT_CHECK([if $1; then :;
	   elif test "X$host" != "X$build" && \
	        { test -x "$1" || test -x "$1"$EXEEXT; }
	   then (exit 77); fi],[$2],[$3],[$4])
])


# LT_AT_TAG(TAG)
# --------------
m4_define([LT_AT_TAG],
[AT_KEYWORDS([$1])
eval `$LIBTOOL --tag=$1 --config | sed -n '/^CC=/ { s/CC/$1/; p;}'`
AT_CHECK([test -n "[$]$1" || (exit 77)])
])


# We use `dnl' in zillions of places...
m4_pattern_allow([^dnl$])

# We exercise these tools.
dnl AT_TESTED([grep autoreconf autom4te automake])

## ----------- ##
## The suite.  ##
## ----------- ##

AT_INIT

# libtoolize tests
m4_include([libtoolize.at])
# Testing func_extract_archives
m4_include([duplicate_members.at])
# Test that inherited_linker_flags in the .la actually gets used.
m4_include([inherited_flags.at])
# convenience archives test
m4_include([convenience.at])
# link order test
m4_include([link-order.at])
# Torturing subdir-objects builds
m4_include([am-subdir.at])
# C++ templates tests
m4_include([template.at])
# stress test
m4_include([stresstest.at])
