dnl Process this file with autoconf to create configure.

AC_INIT(ltmain.in)

AM_INIT_AUTOMAKE(libtool, 1.2e)

# This is a sanity check so we can see which version is used in bug reports.
# It is assumed that we only want to see the date extension for cvs libtool
# versions (i.e. "odd" letters) and not actual alpha releases.
case "$VERSION" in
changequote(,)
  *[acegikmoqsuwy]) 
    TIMESTAMP=`sed 's%.*\$''Revision: \([^$]*\) \$ \$''Date: \([^$]*\) \$.*% (\1 \2)%;t end;d;: end' < ${srcdir}/ChangeLog`
changequote([,])
    banner="Configuring $PACKAGE $VERSION$TIMESTAMP"
    dashes=`echo "$banner" | sed 's/./-/g'`

    # Display an obvious version banner
    echo
    echo $dashes
    echo "$banner"
    echo $dashes
    echo
    ;;
esac    

aclocaldir='${datadir}/aclocal'
AC_SUBST(aclocaldir)

dnl Use the specified CC, RANLIB, and LD while running ltconfig.
AC_PROG_CC
AC_PROG_RANLIB
AM_PROG_LD
AM_PROG_NM
AC_PROG_LN_S

dnl For the `lineno' script (which puts line numbers into `ltconfig').
AC_PROG_AWK

dnl all subdirectories that are configured on demand, but that must be
dnl included in the distribution
CONF_SUBDIRS="libltdl demo mdemo cdemo depdemo"
AC_SUBST(CONF_SUBDIRS)

ACINCLUDE_M4_LIST="\$(srcdir)/acinclude.m4 "
DIST_MAKEFILE_LIST=
for dir in $CONF_SUBDIRS; do
  ACINCLUDE_M4_LIST="$ACINCLUDE_M4_LIST\$(srcdir)/$dir/acinclude.m4 "
  DIST_MAKEFILE_LIST="$DIST_MAKEFILE_LIST$dir/Makefile "
done
AC_SUBST(ACINCLUDE_M4_LIST)
AC_SUBST(DIST_MAKEFILE_LIST)

AC_OUTPUT([Makefile doc/Makefile tests/Makefile])

# Local Variables:
# mode:shell-script
# sh-indentation:2
# End:
